<!DOCTYPE html>
<html lang=en>
           <head>
            <meta charset=UTF-8>
            <title>setf values</title>
            <meta name=viewport
                  content="width=device-width, initial-scale=1.0">
            <link rel=stylesheet
                  href=https://cdn.jsdelivr.net/npm/bootstrap@4.5.3/dist/css/bootstrap.min.css
                  integrity=sha384-TX8t27EcRE3e/ihU7zmQxVncDAy5uIKz4rEkgIXeMed4M0jlfIDPvg6uqKI2xXr2
                  crossorigin=anonymous>
            <style>
section.tree {
    padding-left: 2em;
}
section.tree:first-child {
    padding-left: 0;
}
.article-link {
  margin-bottom: 1em;
}
</style>
<!-- Google Analytics -->
<script async src="https://www.googletagmanager.com/gtag/js?id=G-9X3G9MMWZP"></script>
<script>
  window.dataLayer = window.dataLayer || [];
  function gtag(){dataLayer.push(arguments);}
  gtag('js', new Date());

  gtag('config', 'G-9X3G9MMWZP');
</script>

           </head>
           <body>
            <header class="d-flex justify-content-center">
             <nav
                  class="navbar navbar-light bg-light w-100 mx-5 mb-3">
              <a class=navbar-brand href="/">Lisp HUG Maillist Archive</a>
             </nav>
            </header>
            <div class="d-flex justify-content-center">
             <div class="w-100 mx-5 px-3">
              <section class=tree>
               <article class=email>
                <h1>setf values</h1><HTML>
<HEAD>
<TITLE>setf values</TITLE>
</HEAD>
<BODY>
<FONT FACE="Verdana">Hello,<BR>
<BR>
The CAPI functions (setf pinboard-pane-position) and (setf pinboard-pane-size) have a particularity I&#8217;d never seen elsewhere :<BR>
<BR>
(setf (pinboard-pane-position pane) (values x y))<BR>
Witch return (pinboard-pane-position pane) =&gt; x, y<BR>
<BR>
My question is simple (and maybe very na&iuml;ve) : <BR>
How to write a setf function that accept a (values x y) as his entry ? I didn&#8217;t find the solution nor in the doc or by testing.<BR>
<BR>
Thanks<BR>
<BR>
Denis<BR>
<BR>
<BR>
----------------------------------------------------<BR>
Denis Pousseur<BR>
6 clos du Drossart<BR>
1180 Bruxelles, Belgique<BR>
<BR>
Mail : &nbsp;denis.pousseur@compositeurs.be<BR>
Website : http://compositeurs.be<BR>
----------------------------------------------------<BR>
<BR>
</FONT>
</BODY>
</HTML>



               </article>
               <section class=tree>
                <article class=email>
                 <h1>Re: setf values</h1>
                 <pre>
On Thu, 23 Mar 2006 11:21:16 +0100, Denis Pousseur &lt;denis.pousseur@compositeurs.be&gt wrote:

&gt How to write a setf function that accept a (values x y) as his entry
&gt ? I didn¹t find the solution nor in the doc or by testing.

You need a macro, not a function.

  &lt;http://www.lispworks.com/documentation/HyperSpec/Body/m_defi_3.htm&gt

Here's a toy example that worked for me:

  CL-USER 38 &gt (defun frob (list)
                 "Returns the first two elements of LIST as two values."
                 (values (first list) (second list)))
  FROB

  CL-USER 39 &gt (define-setf-expander frob (place &amp;environment env)
                 (multiple-value-bind (dummies vals newval setter getter)
                     (get-setf-expansion place env)
                   (let ((store-1 (gensym))
                         (store-2 (gensym)))
                     (values dummies
                             vals
                             `(,store-1 ,store-2)
                             `(setf (values (first ,getter) (second ,getter))
                                    (values ,store-1 ,store-2))
                             getter))))
  FROB

  CL-USER 40 &gt (defparameter *a* (list 1 2 3))
  *A*

  CL-USER 41 &gt (defparameter *b* (list *a* 'foo))
  *B*

  CL-USER 42 &gt (frob (first *b*))
  1
  2

  CL-USER 43 &gt (setf (frob (first *b*)) (values 42 43))
  42
  43

  CL-USER 44 &gt (frob (first *b*))
  42
  43

  CL-USER 45 &gt *a*
  (42 43 3)

  CL-USER 46 &gt *b*
  ((42 43 3) FOO)

  CL-USER 47 &gt (setf (frob *a*) (values (list 1 2) (list 3 4)))
  (1 2)
  (3 4)

  CL-USER 48 &gt (setf (second (nth-value 1 (frob (first *b*)))) 69)
  69

  CL-USER 49 &gt *a*
  ((1 2) (3 69) 3)

HTH,
Edi.


</pre>
                </article>
               </section>
               <section class=tree>
                <article class=email>
                 <h1>Re: setf values</h1>
                 <pre>
On Thu, 23 Mar 2006 12:00:28 +0100, Edi Weitz &lt;edi@agharta.de&gt wrote:

&gt   CL-USER 39 &gt (define-setf-expander frob (place &amp;environment env)
&gt                  (multiple-value-bind (dummies vals newval setter getter)
&gt                      (get-setf-expansion place env)
&gt                    (let ((store-1 (gensym))
&gt                          (store-2 (gensym)))
&gt                      (values dummies
&gt                              vals
&gt                              `(,store-1 ,store-2)
&gt                              `(setf (values (first ,getter) (second ,getter))
&gt                                     (values ,store-1 ,store-2))
&gt                              getter))))

Should have been

  (define-setf-expander frob (place &amp;environment env)
    (multiple-value-bind (dummies vals newval setter getter)
        (get-setf-expansion place env)
      (let ((store-1 (gensym))
            (store-2 (gensym)))
        (values dummies
                vals
                `(,store-1 ,store-2)
                `(setf (values (first ,getter) (second ,getter))
                       (values ,store-1 ,store-2))
                `(frob ,getter)))))


</pre>
                </article>
                <section class=tree>
                 <article class=email>
                  <h1>Re: setf values</h1>
                  <pre>
It works perfectly, thanks a lot !


Le 23/03/06 12:09, «&nbsp;Edi Weitz&nbsp;» &lt;edi@agharta.de&gt a écrit&nbsp;:

&gt 
&gt On Thu, 23 Mar 2006 12:00:28 +0100, Edi Weitz &lt;edi@agharta.de&gt wrote:
&gt 
&gt&gt   CL-USER 39 &gt (define-setf-expander frob (place &amp;environment env)
&gt&gt                  (multiple-value-bind (dummies vals newval setter getter)
&gt&gt                      (get-setf-expansion place env)
&gt&gt                    (let ((store-1 (gensym))
&gt&gt                          (store-2 (gensym)))
&gt&gt                      (values dummies
&gt&gt                              vals
&gt&gt                              `(,store-1 ,store-2)
&gt&gt                              `(setf (values (first ,getter) (second ,getter))
&gt&gt                                     (values ,store-1 ,store-2))
&gt&gt                              getter))))
&gt 
&gt Should have been
&gt 
&gt (define-setf-expander frob (place &amp;environment env)
&gt   (multiple-value-bind (dummies vals newval setter getter)
&gt       (get-setf-expansion place env)
&gt     (let ((store-1 (gensym))
&gt           (store-2 (gensym)))
&gt       (values dummies
&gt               vals
&gt               `(,store-1 ,store-2)
&gt               `(setf (values (first ,getter) (second ,getter))
&gt                      (values ,store-1 ,store-2))
&gt               `(frob ,getter)))))
&gt 
&gt 



----------------------------------------------------
Denis Pousseur
6 clos du Drossart
1180 Bruxelles, Belgique

Mail :  denis.pousseur@compositeurs.be
Website : http://compositeurs.be
----------------------------------------------------




</pre>
                 </article>
                </section>
               </section>
              </section>
             </div>
            </div>
            <footer class="d-flex justify-content-center">
             <div>
              Updated at: 2020-12-10 08:48 UTC
             </div>
            </footer>
           </body>
          </html>